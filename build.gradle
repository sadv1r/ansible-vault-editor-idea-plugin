import org.jetbrains.changelog.Changelog
import org.jetbrains.intellij.platform.gradle.TestFrameworkType

import static org.jetbrains.changelog.ExtensionsKt.markdownToHTML

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '2.2.21'
    id 'org.jetbrains.intellij.platform' version '2.10.5'
    id 'org.jetbrains.changelog' version '2.5.0'
}

group = 'ru.sadv1r'
version = properties['pluginVersion']

repositories {
    mavenCentral()

    intellijPlatform {
        defaultRepositories()
    }
}

dependencies {
    implementation('de.rtner:PBKDF2:1.1.4') {
        exclude group: 'org.picketbox', module: 'picketbox'
    }

    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation 'com.github.stefanbirkner:system-lambda:1.2.1'
    testImplementation 'junit:junit:4.13.2'
    // https://plugins.jetbrains.com/docs/intellij/tools-intellij-platform-gradle-plugin-faq.html#missing-opentest4j-dependency-in-test-framework
    testImplementation 'org.opentest4j:opentest4j:1.3.0'

    // IntelliJ Platform Gradle Plugin Dependencies Extension - read more: https://plugins.jetbrains.com/docs/intellij/tools-intellij-platform-gradle-plugin-dependencies-extension.html
    intellijPlatform {
        create(providers.gradleProperty('platformType'), providers.gradleProperty('platformVersion'))

        bundledPlugin('org.jetbrains.plugins.yaml')

        testFramework(TestFrameworkType.Platform.INSTANCE)
    }
}

kotlin {
    jvmToolchain(21)
}

test {
    useJUnit()
}

// See https://github.com/JetBrains/intellij-platform-gradle-plugin/
intellijPlatform {
    pluginConfiguration {
        name = 'Ansible Vault Editor'

        description = providers.fileContents(layout.projectDirectory.file("README.md")).asText.map {
            def start = "<!-- Plugin description -->"
            def end = "<!-- Plugin description end -->"
            def lines = it.readLines()

            if (!lines.contains(start) || !lines.contains(end)) {
                throw new GradleException("Plugin description section not found in README.md:\n$start ... $end")
            }

            def startIdx = lines.indexOf(start)
            def endIdx = lines.indexOf(end)
            def content = lines.subList(startIdx + 1, endIdx).join("\n")

            markdownToHTML(content, "")
        }

        changeNotes = provider {
            changelog.renderItem(
                    changelog
                            .getLatest()
                            .withHeader(false)
                            .withEmptySections(false),
                    Changelog.OutputType.HTML
            )
        }

        ideaVersion {
            sinceBuild = "242"
            // No untilBuild - allows forward compatibility
        }
    }

    pluginVerification {
        ides {
            recommended()
        }
    }
}

changelog {
    version = project.version
    headerParserRegex = ~/(\d+\.\d+)/
}

publishPlugin {
    dependsOn("patchChangelog")
    token = System.getenv("PUBLISH_TOKEN")
}