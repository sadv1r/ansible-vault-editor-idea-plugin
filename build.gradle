import org.jetbrains.changelog.Changelog

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.9.20'
    id 'org.jetbrains.intellij' version '1.16.0'
    id 'org.jetbrains.changelog' version '2.2.0'
}

group 'ru.sadv1r'
version = properties['pluginVersion']

repositories {
    mavenCentral()
}

dependencies {
    implementation('de.rtner:PBKDF2:1.1.4') {
        exclude group: 'org.picketbox', module: 'picketbox'
    }

    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation 'org.jetbrains.kotlin:kotlin-test-testng'
    testImplementation 'com.github.stefanbirkner:system-lambda:1.2.1'
}

kotlin {
    jvmToolchain(11)
}

test {
    useJUnit()
}
tasks.register('integrationTest', Test) {
    useTestNG()
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    plugins = ['yaml']
    version = '2022.1'
    pluginName = 'Ansible Vault Editor'
    updateSinceUntilBuild = false
}

changelog {
    version = project.version
    headerParserRegex = ~/(\d+\.\d+)/
}

patchPluginXml {
    //https://plugins.jetbrains.com/docs/intellij/build-number-ranges.html#intellij-platform-based-products-of-recent-ide-versions
    sinceBuild = "203"

    pluginDescription = """
Helps you create and edit Ansible Vaults
<br/>
<br/>
<a href="https://github.com/sadv1r/ansible-vault-editor-idea-plugin">GitHub</a> |
<a href="https://github.com/sadv1r/ansible-vault-editor-idea-plugin/issues">Issues</a>
<br/>
<br/>
<b>Features</b>
<ul>
    <li>Encrypt <a href="https://docs.ansible.com/ansible/latest/user_guide/vault.html#encrypting-files-with-ansible-vault">file as Ansible Vault</a></li>
    <li>Encrypt <a href="https://docs.ansible.com/ansible/latest/user_guide/vault.html#encrypting-individual-variables-with-ansible-vault">individual YAML property</a></li>
    <li>Modify existing Vault with auto encryption/decryption and protection from accidental pushing of sensitive data</li>
    <li>Remember each vault <a href="https://www.jetbrains.com/help/idea/reference-ide-settings-password-safe.html">password</a> or use <a href ="https://docs.ansible.com/ansible/latest/reference_appendices/config.html#default-vault-password-file">DEFAULT_VAULT_PASSWORD_FILE</a> configuration</li>
    <li>Re-encrypt vault with new password</li>
</ul>
"""

    changeNotes.set(
            changelog.renderItem(
                    changelog
                            .getLatest()
                            .withHeader(false)
                            .withEmptySections(false),
                    Changelog.OutputType.HTML
            )
    )
}

publishPlugin {
    dependsOn("patchChangelog")
    token = System.getenv("PUBLISH_TOKEN")
}